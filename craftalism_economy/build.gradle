plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'io.github.HenriqueMichelini'
version = '1.2-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT"

    // Testing
    testImplementation platform("org.junit:junit-bom:5.13.4")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation "org.mockito:mockito-core:5.19.0"
    testImplementation 'org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.0.0'

    // Shaded libs
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
}

shadowJar {
    exclude 'org/mockbukkit/**'
    exclude 'junit/**'
    exclude 'org/junit/**'
    exclude 'org/mockito/**'

    relocate 'com.github.benmanes.caffeine', 'io.github.HenriqueMichelini.craftalism_economy.libs.caffeine'
    relocate 'org.apache.http', 'io.github.HenriqueMichelini.craftalism_economy.libs.apache.http'

    archiveClassifier.set('shadow')
}


configurations {
    testImplementation {
        extendsFrom = []
    }
}

test {
    useJUnitPlatform()
    testLogging { events "passed", "skipped", "failed" }
    jvmArgs "-XX:+EnableDynamicAgentLoading", "-Djdk.instrument.traceUsage"
}

def targetJavaVersion = 21
java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

processResources {
    inputs.properties(version: version)
    filteringCharset 'UTF-8'

    filesMatching('plugin.yml') {
        expand(version: version)
    }
}
